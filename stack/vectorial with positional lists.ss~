#lang r6rs

;-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-
;-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-
;-*-*                                                                 *-*-
;-*-*     stack (Vectorial Implementation with positional lists)      *-*-
;-*-*                                                                 *-*-
;-*-*                       Wolfgang De Meuter                        *-*-
;-*-*                  2009  Software Languages Lab                   *-*-
;-*-*                   Vrije Universiteit Brussel                    *-*-
;-*-*                                                                 *-*-
;-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-
;-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-

(library
 (stack)
 (export new stack? push! pop! top empty? full?)
 (import (rnrs base (6))
         (prefix (a-d positional-list adt) plist:))
 
 (define stack-tag 'vector-stack)
 (define (make)
   (list stack-tag (plist:new eq?)))
 (define (plist stack)
   (cadr stack))
 
 (define (new)
   (make))
 
 (define (stack? any)
   (and (pair? any)
        (eq? (car any) stack-tag)))
 
 (define (push! stack val)
   (plist:add-after! (plist stack) val)
   stack)
 
 (define (top stack)
   (define plst (plist stack))
   (if (= (plist:length plst) 0)
     (error "stack empty (top)" stack))
   (plist:peek plst (plist:last plst)))
 
 (define (pop! stack)
   (define plst (plist stack))
   (if (= (plist:length plst) 0)
     (error "stack empty (pop)" stack))
   (let ((val (plist:peek 
               plst 
               (plist:last plst))))
     (plist:delete! 
      plst (plist:last plst))
     val))
 
 (define (empty? stack)
   (plist:empty? (plist stack)))
 
 (define (full? stack)
   (plist:full? (plist stack))))